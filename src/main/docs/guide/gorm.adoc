GORM works with any JDBC-compatible database, which includes Postgres
(as well as over 200 other databases). To begin using Postgres with our
new Grails app, we have 2 steps to complete:

Step #1::
Install the JDBC driver in our `server` project. Edit
`server/build.gradle`, and find the section named `dependencies`. Add
the following line of code:
`runtime 'org.postgresql:postgresql:9.4.1212'` This will tell Gradle to
download version 9.4.1212 of the `org.postgresql.postgresql` library
from the Maven Central repository, and install it in our app.

[source, groovy]
./server/build.gradle
----
include::{sourceDir}/server/build.gradle[indent=0,lines=59]
----

Step #2:: Configure GORM to use our PostgreSQL database instead of the default
H2 database. Edit `server/grails-app/conf/application.yml`, scroll down
to the section starting with `datasource`, and replace it with the
following content:

[source, yml]
./server/grails-app/conf/application.yml
----
include::{sourceDir}/server/grails-app/conf/application.yml[indent=0,lines=81..93]
----

Now our Grails app is connected to our database, but we haven’t created
any tables yet. With Grails, there’s no need to create the database
schema manually (although you certainly can do so if you want). Instead,
we’ll specify our domain model in code, by writing *Domain Classes*.

By convention, Grails will load any Groovy classes located under
`grails-app/domain` as Domain Classes. This means that GORM will map
these classes to tables in the database, and map the properties of these
classes to columns in the respective tables. Optionally, GORM will
create these tables for us, which we have already enabled in our
`application.yml` file with the `dbCreate: update` setting.

This means it’s actually quite trivial to set up the database schema
from the previous application. For each of the tables we need in our
app, we will create a domain class under the `grails-app/domain`
directory.

Run the following commands:

[source, bash]
----
~ ./grailsw create-domain-class ice.cream.IceCream
~ ./grailsw create-domain-class ice.cream.User
~ ./grailsw create-domain-class ice.cream.UserIceCream
----

These commands will generate three Groovy classes, under
`grails-app/domain/ice/cream`. Edit these files with the following
content:


[source, groovy]
./server/grails-app/domain/ice/cream/IceCream.groovy
----
include::{sourceDir}/server/grails-app/domain/ice/cream/IceCream.groovy[]
----

[source, groovy]
./server/grails-app/domain/ice/cream/User.groovy
----
package ice.cream

class User {
    String username
    String password
}
----

TIP: You may have noticed we have not encrypted our `password` column
- don’t worry, we’ll get to that later on.

[source, groovy]
./server/grails-app/domain/ice/cream/UserIceCream.groovy
----
include::{sourceDir}/server/grails-app/domain/ice/cream/UserIceCream.groovy[]
----
<1> Because this domain class represents a join table for our `User` and `IceCream`
classes.

==== Package Naming

As is common in Java projects, we have created a “package” for our
domain classes. Packages help distinguish our classes from classes from
libraries or plugins that we might use later. The package is reflected
in the directory structure as well: these two files will be created
under `grails-app/domain/ice/cream/`.

TIP: Why are we using period instead of dash seprators, as was doing in the original article?
In Java it is generally considered against convention to use dashes (hyphen) in package names.
http://docs.oracle.com/javase/specs/jls/se8/html/jls-6.html#jls-6.1[See this link for details on the Java naming conventions].

==== Run the app

If you were to start up your app now, Grails will connect to the
Postgres database, create the tables and columns needed to persist your
domain objects. Of course, there would be no data in the database
initially. We will solve that issue shortly, but for now, we can run a
Grails command that will give as an interactive console where we can
create, update, delete and query our domain objects.

Run the following command:

[source, bash]
----
~ ./grailsw console
----

(If you have Grails installed locally on your machine, you can run the
`grails` command directly, e.g.: `grails console`. However, Grails
projects include the `grailsw` "wrapper" command which will install the
correct version of Grails for you.)

Now you should see the Grails Console. You can import any classes from
your project (both your own code as well as any dependencies) and run
any Groovy code you'd like.

The following code will show how to accomplish the database operations
from the previous "Web App" article, using GORM and the Groovy language:

[source,groovy]
----
import ice.cream.*

// Delete all rows from these tables:
// user and ice_cream via HQL updates (efficient for batch operations)
IceCream.executeUpdate("delete from IceCream")
User.executeUpdate("delete from User")

// Insert three new rows corresponding to three flavors.
def iceCreams = ['vanilla', 'chocolate', 'strawberry'].collect { flavor ->
    new IceCream(flavor: flavor).save(flush: true)
}

// Get an array of the ids of the new rows.
def ids = iceCreams*.id
println "Inserted records with ids ${ids.join(',')}"

// Delete the first row (vanilla)
def vanilla = IceCream.get(ids[0])
vanilla.delete()

// Change the flavor of the second row (chocolate) to "chocolate chip".
def chocolate = IceCream.findByFlavor('chocolate')
chocolate.flavor = 'chocolate chip'
chocolate.save(flush: true)

// Get all the rows in the table.
iceCreams = IceCream.list()

// Output their ids and flavors.
iceCreams.each { iceCream ->
    println "${iceCream.id}: ${iceCream.flavor}"
}
----

Enter the above code into the Grails Console (launched with the previous
command), and click the "Run" button to execute the script. If you like,
you can save the script to be reused later (note that this Groovy script
will only work in the Grails Console, not via the "plain" Groovy Console
or Groovy compiler (`groovyc`) - this is because our domain classes need
to be loaded by Grails in order for this code to work).

You might think this method could be used to populate our database with
some initial data, and you'd be correct - any inserts/updates we make in
the Console are persisted to the database we configured in our
`application.yml` file. However, Grails provides a `BootStrap.groovy`
file which is much better suited to this task.

Edit the file `server/grails-app/init/ice/cream/BootStrap.groovy` and add the
following code:

[source, groovy]
./server/grails-app/init/ice/cream/BootStrap.groovy
----
include::{sourceDir}/server/grails-app/init/ice/cream/BootStrap.groovy[]
----

As you can see, we simply borrowed some code from our script and used it
in the `init` method, which Grails will execute during application
startup.
